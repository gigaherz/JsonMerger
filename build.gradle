buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

version = '1.3.0'
group = 'dev.gigaherz.jsonmerger' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'JsonMerger-lite'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    maven { url 'https://www.dogforce-games.com/maven/' }
    mavenLocal()
}

sourceSets {
    debug
}

minecraft {
    mappings channel: 'official', version: '1.19'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'CLASSDUMP'
            property 'forge.logging.console.level', 'trace'

            property 'bsl.debug', 'true'

            mods {
                thismod {
                    source sourceSets.main
                    source sourceSets.debug
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                    source sourceSets.debug
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19-41.0.110'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testImplementation "com.google.guava:guava:29.0-jre"
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
            "Specification-Title": "jsonmerger",
            "Specification-Vendor": "gigaherz",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"gigaherz",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            //artifact sourceJar

            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if(dep.groupId.last().value().last() in ["net.minecraftforge"]) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}
